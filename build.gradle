import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'java-library'
	id 'org.springframework.boot' version '3.2.3' apply false
	id 'io.spring.dependency-management' version '1.1.4'
	id 'jacoco'
	id 'maven-publish'
	id 'com.github.jmongard.git-semver-plugin' version '0.11.0'
}

semver {
	noDirtyCheck = true
}

group = 'it.magiavventure'
version = semver.version

java {
	sourceCompatibility = '17'
}

dependencyManagement {
	imports {
		mavenBom SpringBootPlugin.BOM_COORDINATES
	}
}

allprojects {
	gradle.projectsEvaluated {
		tasks.withType(JavaCompile).tap {
			configureEach {
				options.compilerArgs << '-parameters'
			}
		}
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	validationVersion = '3.2.3'
}

dependencies {
	api 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'com.fasterxml.jackson.core:jackson-annotations'
	implementation "org.springframework.boot:spring-boot-starter-validation:$validationVersion"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/Magiavventure/magiavventure-registry")
			credentials {
				username = project.findProperty("user") ?: System.getenv("USERNAME")
				password = project.findProperty("token") ?: System.getenv("TOKEN")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			from(components.java)
		}
	}
}